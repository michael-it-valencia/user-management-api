plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.1'
	id 'io.spring.dependency-management' version '1.1.5'
	id("org.openapi.generator") version "7.6.0"
}

group = 'com.michael.valencia.prototypes'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

// OPENAPI GENERATED SOURCESET
sourceSets {
    main {
        java {
            srcDirs("$buildDir/generated/src/main/java")
        }
    }
}

openApiGenerate {
    generatorName.set("spring")
    inputSpec.set("$rootDir/src/main/resources/spec/openapi.yaml")
    outputDir.set("$buildDir/generated")
    apiPackage.set("com.michael.valencia.prototypes.api.openapi")
    modelPackage.set("com.michael.valencia.prototypes.api.openapi.model")
    modelNameSuffix.set("Dto")
    configOptions = [
            useJakartaEe: "true",
            interfaceOnly: "true"   
        ]
}

tasks.compileJava{
    dependsOn "openApiGenerate"
}
